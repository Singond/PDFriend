apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.izpack'

buildscript {
	repositories {
		jcenter()
		mavenLocal()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
	}
}

sourceSets {
	dev
}


repositories {
	jcenter()
	mavenLocal()
}

dependencies {
	implementation 'com.github.singond:collections:0.4.+'
	implementation 'com.github.singond:geometry:0.6.+'
	implementation 'org.apache.pdfbox:pdfbox:2.0.6'
	implementation 'org.apache.logging.log4j:log4j-api:2.8.2'
	implementation 'org.apache.logging.log4j:log4j-core:2.8.2'
	implementation 'org.apache.commons:commons-collections4:4.1'
	implementation 'com.beust:jcommander:1.72'

	testCompile 'junit:junit:4.12'

	izpack 'org.codehaus.izpack:izpack-standalone-compiler:4.3.4'
}

eclipse {
	classpath {
		downloadJavadoc = true
	}
}

jar {
	manifest {
		attributes (
			'Main-Class': 'com.github.singond.pdfriend.Application',
			'Built-By': 'singon'
		)
	}
	from {
		configurations.compileClasspath.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
}

/** Auxiliary files to be used during the installation */
def auxFiles = "build/installers"
/** The files to be installed */
def instFiles = "build/distfiles"

izpack {
	baseDir = file("build")
	installFile = file("install.xml")
	outputFile = file("dist/pdfriend-installer.jar")
}

task installerScripts (type: Copy) {
	description = "Copies the auxiliary installer scripts into the directory used by installer"
	from ("ext/installers")
	into auxFiles
}

task launchers (type: Copy) {
	description = "Copies the launcher scripts into the directory used by installer"
	from ("ext/launchers")
	into instFiles
}

izPackCreateInstaller.dependsOn installerScripts
izPackCreateInstaller.dependsOn launchers

/**
 * Creates an installer from the build/distfiles directory.
 */
task installer(dependsOn: izPackCreateInstaller) {
	description = "Create a standalone offline installer"
	group = "Distribution"
}